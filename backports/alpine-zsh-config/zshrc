# System-wide .zshrc file for zsh(1), sourced only for interactive shells.
#
# Global order: zshenv, zprofile, zshrc, zlogin

: ${ZSH_PLUGIN_DIRS:="${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins"}

# TIP: If you want to automatically use all ZSH plugins installed from Alpine
# packages, add `USE_GLOBAL_PLUGINS=yes` to your $ZDOTDIR/.zshenv.
if [[ "${USE_GLOBAL_PLUGINS-}" = [1ty]* ]]; then
	ZSH_PLUGIN_DIRS+=('/usr/share/zsh/plugins')
fi

# Find plugins (per Oh-My-Zsh convention) under $ZSH_PLUGIN_DIRS (array of
# paths) and add them to $fpath.
_plugins=()
for _dir (${ZSH_PLUGIN_DIRS-}); do
	for _name ("$_dir"/*(/N:t)); do  # equivalent of 'basename $i'
		if [[ -f "$_dir/$_name/$_name.plugin.zsh" ]]; then
			_plugins+=("$_dir/$_name/$_name.plugin.zsh")
			fpath=("$_dir/$_name" $fpath)
		fi
	done
done

# Source each <name>.zsh script in /etc/zsh/zshrc.d/ which <name> is not
# included in $GLOBAL_ZSHRC_BLACKLIST.
#
# TIP: If you want to prevent some scripts in /etc/zsh/zshrc.d from being
# loaded, say a.zsh and b.zsh, add `GLOBAL_ZSHRC_BLACKLIST=(a b)` to your
# $ZDOTDIR/.zshenv ($XDG_CONFIG_HOME/zsh/.zshenv).
GLOBAL_ZSHRC_BLACKLIST+=()
_scripts=(/etc/zsh/zshrc.d/*.zsh(N:t:r))  # equivalent of 'basename $i .zsh'
for _name (${_scripts:|GLOBAL_ZSHRC_BLACKLIST}); do  # :| is a set difference A \ B
	source "/etc/zsh/zshrc.d/$_name.zsh"
done

# Source all plugins found in $ZSH_PLUGIN_DIRS.
for _plugin ($_plugins); do
	source "$_plugin"
done

unset _dir _name _plugin _plugins _scripts

# vim: set ft=zsh ts=4:
